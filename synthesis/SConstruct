
import os
import sys

# Put the package name here, e.g. casa, tables
PACKAGE = "synthesis"
DEPS = ["casa", "mirlib", "tables", "scimath_f", "scimath",
        "measures", "fits", "ms", "msvis", 
        "coordinates", "components", "lattices", "images", "calibration"]

# general options
opts = Options( 'options.cfg', ARGUMENTS )
opts.Add(PathOption("casacoreroot", "The location of casacore/casa",
                    "/usr/local"))
opts.Add(PathOption("casarestroot", "The location of casarest",
                    "/usr/local"))
opts.Add(("lapackroot", 
          "The root directory where lapack is installed", None))
opts.Add(("lapacklibdir", "The lapack library location", None))
opts.Add(("lapacklib",
          "The lapack library name (e.g. for specialized AMD libraries",
          None))
opts.Add(("blasroot", 
          "The root directory where blas is installed", None))
opts.Add(("blaslibdir", "The blas library location", None))
opts.Add(("blaslib",
          "The blas library name (e.g. for specialized AMD libraries",
          None))
opts.Add(("cfitsioroot", 
	  "The root directory where cfistio is installed", None))
opts.Add(("cfitsiolibdir", "The cfitsio library location", None))
opts.Add(("cfitsioincdir", "The cfitsio header location", None))
opts.Add(("wcsroot", 
	  "The root directory where wcs is installed", None))
opts.Add(("wcslibdir", "The wcs library location", None))
opts.Add(BoolOption("enable_hdf5", "Turn on hdf5 support", False))
opts.Add(("hdf5root", 
	  "The root directory where hdf5 is installed", None))
opts.Add(("hdf5libdir", "The hdf5 library location", None))

env = Environment(
		  ENV = { 'PATH' : os.environ[ 'PATH' ],
			  'HOME' : os.environ[ 'HOME' ]			  
			  },
		  options=opts
		  )
# keep a local sconsign database, rather than in very directory
env.SConsignFile()

env["PACKAGE"] = PACKAGE

# use directory from casacore/casa installation
env["casaincdir"] = [os.path.join(env["casacoreroot"],"include","casacore")]
env["casalibdir"] = [os.path.join(env["casacoreroot"],"lib")]
env["casashrdir"] = [os.path.join(env["casacoreroot"],"share","casacore")]

env["casarestincdir"] = [os.path.join(env["casarestroot"],"include","casarest")]
env["casarestlibdir"] = [os.path.join(env["casarestroot"],"lib")]
env["casarestshrdir"] = [os.path.join(env["casarestroot"],"share","casarest")]

env.Tool('casaoptions', env["casashrdir"])
# Add common options
env.AddCommonOptions(opts)
env.AddFortranOptions(opts)
env.Tool('installer', env["casashrdir"])
# add installer options, e.g. prefix
env.AddInstallerOptions( opts )
# add them into environment
opts.Update( env )
# cache them for the next run
opts.Save( 'options.cfg', env)
Help( opts.GenerateHelpText( env ) )
env.Tool('buildenv', env["casashrdir"])
env.Tool('utils', env["casashrdir"])
env.Tool('casa', env["casashrdir"])
env.Tool('assaytest', env["casashrdir"])
env["ASSAYCOM"] = os.path.join(env["casashrdir"][0], "casacore_assay")

# Auto configure
if not env.GetOption('clean'):
    conf = Configure(env)
    conf.env.CheckFortran(conf)
    if not conf.CheckLib("m", autoadd=0): Exit(1)
    conf.env.PrependUnique(LIBS=['m'])  
    # test for blas/lapack
    blasname = conf.env.get("blaslib", "blas").split(",")
    conf.env.AddCustomPackage("blas")
    if not conf.CheckLib(blasname, autoadd=0): Exit(1)
    conf.env.PrependUnique(LIBS=blasname)
    lapackname = conf.env.get("lapacklib", "lapack").split(",")
    conf.env.AddCustomPackage("lapack")
    if not conf.CheckLib(lapackname, autoadd=0): Exit(1)
    conf.env.PrependUnique(LIBS=lapackname)
    # test for cfitsio
    conf.env.AddCustomPackage('cfitsio')
    if not conf.CheckLib('cfitsio', autoadd=0):
	Exit(1)
    conf.env.PrependUnique(LIBS=['cfitsio'])  

    conf.env.AddCustomPackage('wcs')
    if not conf.CheckLibWithHeader('wcs', 'wcslib/wcs.h', language='c',
				   autoadd=0):
	Exit(1)
    conf.env.PrependUnique(LIBS=['wcs'])

    if conf.env["enable_hdf5"]:
        conf.env.AddCustomPackage('hdf5')
        if conf.CheckLib('hdf5', autoadd=0):
            conf.env.PrependUnique(LIBS=['hdf5'])  
            conf.env.PrependUnique(CPPFLAGS=['-DHAVE_HDF5'])
        else:
            Exit(1)
    conf.env.PrependUnique(CPPFLAGS=['-DAIPS_NO_TEMPLATE_SRC','-DCASACORE_NO_AUTO_TEMPLATES'])
    env = conf.Finish()

# create the installer which handles installing the final build
installer = env.Installer()

# to find package based includes
env.Append(CPPPATH=['#'])
env.AppendUnique(CPPPATH=env["casaincdir"])
env.AppendUnique(CPPPATH=env["casarestincdir"])
env.AppendUnique(LIBPATH=env["casalibdir"])
env.AppendUnique(LIBPATH=env["casarestlibdir"])
for dep in DEPS:
    env.Prepend(LIBS=["casa_"+dep])


# Replace some builder commands (lex,yacc) with custom versions
env.CustomCasaCom()
for bopt in env["build"]:
    # create an environment copy with the dbg/opt compiler flags
    buildenv = env.BuildEnv(bopt)
    buildenv["buildtype"] = bopt
    # buildir name
    buildenv["BUILDDIR"] = Dir("#/build_%s/%s" % (env.PlatformIdent(), bopt))
    buildenv["BUILDDIR_FORT"] = Dir("#/build_%s/%s/%s" % (env.PlatformIdent(), 
						      bopt, "fortran"))
    buildenv["BUILDDIR_APPS"] = Dir("#/build_%s/%s/%s" % (env.PlatformIdent(), 
						      bopt, "apps"))

    env.SConscript(["%s/SConscript" % env["PACKAGE"]], 
		   build_dir= buildenv["BUILDDIR"],
		   duplicate=0, exports=["buildenv", "installer"]) 
    env.SConscript(["%s/SConscript" % "fortran"],
		   duplicate=0, exports=["buildenv", "installer"],
		   build_dir=buildenv["BUILDDIR_FORT"])
    env.SConscript(["%s/SConscript" % "apps"],
		   duplicate=0, exports=["buildenv", "installer"],
		   build_dir=buildenv["BUILDDIR_APPS"])
